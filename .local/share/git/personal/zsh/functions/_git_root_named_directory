#!/usr/bin/env zsh

# A function that registers a dynamic named directory for the root 
# of the current git repository

# See the heading "Dynamic named directories" under `man 1 zshexpn`

emulate -L zsh
local git_root_dirname="g"

# Implementation of the named directory feature
if [[ "$1" = n ]]; then
  [[ "$2" != "$git_root_dirname" ]] && return 1

  local rootdir=
  # Try to get path to the root of the current git repo
  if ! rootdir="$(git rev-parse --show-toplevel 2> /dev/null)" ; then
    return 1
  fi

  typeset -ga reply
  reply=("$rootdir")
# Completion function for dynamic named directory
elif [[ "$1" = c ]]; then
  # Add named directory to completion list if we're in a git direcory
  if git rev-parse --is-inside-work-tree > /dev/null 2>&1 ; then
    # Taken from zcomet
    # https://github.com/agkozak/zcomet/blob/d4fee3f59651812703aae0a34caa67f186a40cfd/zcomet.zsh#L762-L765
    _tags named-directories
    _tags && _requested named-directories expl 'dynamic named directories' &&
      compadd $expl -S\] -- "$git_root_dirname"
    return 1
  fi
fi